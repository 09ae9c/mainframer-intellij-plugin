import java.util.regex.Pattern

buildscript {
    ext.kotlin_version = '1.1.0'

    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.intellij' version "0.2.0"
}
apply plugin: 'java'
apply plugin: 'kotlin'

group 'com.elpassion.mainframerplugin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

sourceSets {
    main {
        java.srcDirs 'src/main/kotlin'
        kotlin.srcDirs 'src/main/kotlin'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDirs 'src/test/kotlin'
        kotlin.srcDirs 'src/test/kotlin'
    }
}

intellij {
    def intellijVersion = '171.3224.1'
    version intellijVersion
    pluginName 'mainframer-integration'
    publish {
        username project.findProperty("MF_PUBLISH_USER_NAME")
        password project.findProperty("MF_PUBLISH_PASSWORD")
        channel project.findProperty("publishChannel")
    }
    patchPluginXml {
        version readVersion()
        sinceBuild "145"
        untilBuild intellijVersion
    }
}

private static String readVersion() {
    def versionFile = new File('version.gradle.properties')
    def matcher = Pattern.compile("version=(\\d+.\\d+.\\d+)").matcher(versionFile.text)
    if (matcher.find()) {
        return matcher.group(1)
    } else {
        throw new RuntimeException("Version not found!")
    }
}

task updateVersion {
    doLast {
        def versionFile = new File('version.gradle.properties')
        def matcher = Pattern.compile('version=(\\d+.\\d+.\\d+)').matcher(versionFile.text)
        if (matcher.find()) {
            def versionText = new StringBuilder(versionFile.text).replace(matcher.start(1), matcher.end(1), versionName)
            versionFile.text = versionText
        }
    }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-runtime', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.1.0'
    compile group: 'com.squareup.retrofit2', name: 'converter-jackson', version: '2.1.0'
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.0.5'
    compile group: 'com.jakewharton.retrofit', name: 'retrofit2-rxjava2-adapter', version: '1.0.0'

    testCompile group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.2.0'
    testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.6.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
}